buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven {
            // 阿里云镜像，方便国内开发
            url = uri("https://maven.aliyun.com/repository/public/")
        }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

apply plugin: 'eclipse'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'java'
apply plugin: 'idea'

version = mod_version
group = 'com.github.tartaricacid'
archivesBaseName = 'touhoulittlemaid-1.20.1-release'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: '2023.08.20-1.20.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            jvmArgs "-XX:+AllowEnhancedClassRedefinition"
            workingDirectory project.file('run/client_a')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                touhou_little_maid {
                    source sourceSets.main
                }
            }
        }

        client2 {
            parent minecraft.runs.client
            workingDirectory project.file('run/client_b')
            args '--username', 'tartaric_acid'
            mods {
                touhou_little_maid {
                    source sourceSets.main
                }
            }
        }

        server {
            jvmArgs "-XX:+AllowEnhancedClassRedefinition"
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'touhou_little_maid'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                touhou_little_maid {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', 'touhou_little_maid', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                touhou_little_maid {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    exclude '.cache/**'
}

mixin {
    add sourceSets.main, "touhou_little_maid.refmap.json"
    config "touhou_little_maid.mixins.json"
}

repositories {
    // 优先本地 libs 文件夹
    flatDir {
        dir 'libs'
    }
    maven {
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url = "https://api.modrinth.com/maven"
    }
    maven {
        url = "https://maven.conczin.net/Artifacts"
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}

jarJar.enable()

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"

    // Opus 音频解码库
    jarJar(minecraftLibrary("org.gagravarr:vorbis-java-core:0.8")) {
        jarJar.ranged(it, "[0.8,)")
    }
    jarJar(minecraftLibrary("io.github.jaredmdobson:concentus:1.0.2")) {
        jarJar.ranged(it, "[1.0.2,)")
    }

    // YAML 库
    jarJar(minecraftLibrary("org.yaml:snakeyaml:2.4")) {
        jarJar.ranged(it, "[2.4,)")
    }

    // 拆分出来的基岩版模型加载库
    jarJar(implementation(fg.deobf("libs:simplebedrockmodel:1.20.1-release-1.0.0"))) {
        jarJar.ranged(it, "[1.0.0,)")
    }

    // 单元测试
    testImplementation 'junit:junit:4.13.2'

    // compile against the JEI API but do not include it at runtime
    compileOnly(fg.deobf("mezz.jei:jei-1.20.1-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-1.20.1-forge-api:${jei_version}"))
    // at runtime, use the full JEI jar for Forge
    runtimeOnly(fg.deobf("mezz.jei:jei-1.20.1-forge:${jei_version}"))

    // rei
    compileOnly fg.deobf("curse.maven:architectury-api-419699:${architectury_api_id}")
    compileOnly fg.deobf("libs:RoughlyEnoughItems-forge:${rei_version}")

    // emi
    // Forge (see below block as well if you use Forge Gradle)
    compileOnly fg.deobf("libs:emi-forge:${emi_version}:api")
    compileOnly fg.deobf("libs:emi-forge:${emi_version}")

    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")

    implementation fg.deobf("curse.maven:jade-324717:${jade_id}")
    implementation fg.deobf("curse.maven:the-one-probe-245211:${top_id}")

    implementation fg.deobf("libs:cloth-config-forge:${cloth_config_forge}")

    runtimeOnly fg.deobf("maven.modrinth:citadel:2.4.9-forge")
    implementation fg.deobf("curse.maven:domestication-innovation-591149:${domestication_innovation_id}")

    implementation fg.deobf("maven.modrinth:slashblade-resharped:1.1.6-1.20.1")

    implementation fg.deobf("curse.maven:iron-chests-228756:${iron_chests_id}")

    implementation fg.deobf("net.conczin:immersive_melodies:${immersive_melodies_version}")

    runtimeOnly fg.deobf("curse.maven:carry-on-274259:${carry_on_id}")

    runtimeOnly "curse.maven:kotlin-for-forge-351264:${kotlin_id}"
    runtimeOnly fg.deobf("curse.maven:libipn-679177:${libipn_id}")
    implementation fg.deobf("curse.maven:inventory-profiles-next-495267:${ipn_id}")

    runtimeOnly fg.deobf("curse.maven:catalogue-459701:${catalogue_id}")

    runtimeOnly fg.deobf("curse.maven:bookshelf-228525:${bookshelf_id}")
    runtimeOnly fg.deobf("curse.maven:enchantment-descriptions-250419:${enchantment_descriptions_id}")

    implementation fg.deobf("curse.maven:timeless-and-classics-zero-1028108:6069384-sources-6069403")

    implementation fg.deobf("maven.modrinth:simple-hats:1.20.1-0.3.2-forge")
    implementation fg.deobf("maven.modrinth:curios:5.9.1+1.20.1-forge")

    compileOnly fg.deobf("maven.modrinth:torohealth-damage-indicators-updated:1.20.1-forge-1")

    implementation fg.deobf("curse.maven:aquaculture-60028:5585290")
    //runtimeOnly fg.deobf("curse.maven:lavafishing-1035887:5432767")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    runtimeOnly fg.deobf("maven.modrinth:better-advancements:0.4.2.10-forge")
    runtimeOnly fg.deobf("maven.modrinth:farmers-delight:1.20.1-1.2.6-forge")
    runtimeOnly fg.deobf("maven.modrinth:ice-and-fire-dragons:2.1.13-1.20.1-beta-5")
    runtimeOnly fg.deobf("maven.modrinth:embeddium:0.3.31+mc1.20.1")
    compileOnly fg.deobf("maven.modrinth:embeddium:0.3.31+mc1.20.1")
    // runtimeOnly fg.deobf('maven.modrinth:oculus:1.20.1-1.7.0')
    // runtimeOnly fg.deobf("curse.maven:maid-storage-manager-1210244:6315697")
}

tasks.compileJava {
    // dsx137: 强制重新编译和生成 refmap.json 文件，避免偶发情况下该文件丢失问题
    outputs.upToDateWhen { false }
}

jar {
    manifest {
        attributes([
                "Implementation-Title"  : project.name,
                "Implementation-Version": "${version}"
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

// 让 idea 主动下载前置库的源码和 Javadoc
// 新版本 idea 默认不会下载这两个，这虽然加快了构建速度，但是不方便调试
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
